plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'bitecode'
version = '0.0.9-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

idea {
	module {
		downloadJavadoc = true
		downloadSources = true
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
    maven {
        name = 'Central Portal Snapshots'
        url = 'https://central.sonatype.com/repository/maven-snapshots/'
    }
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

allprojects {
	apply plugin: 'io.spring.dependency-management'

	dependencyManagement {
		imports {
			mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.4"
		}
		dependencies {
			dependency "com.google.guava:guava:33.4.6-jre"
			dependency "org.apache.commons:commons-lang3:3.14.0"
			dependency "com.github.ben-manes.caffeine:caffeine:3.2.2"
			dependency "org.mapstruct:mapstruct:1.6.3"
			dependency "org.mapstruct:mapstruct-processor:1.6.3"
			dependency "org.mapstruct.extensions.spring:mapstruct-spring-annotations:1.1.3"
			dependency "org.projectlombok:lombok-mapstruct-binding:0.2.0"
			dependency "com.querydsl:querydsl-jpa:5.1.0"
			dependency "com.querydsl:querydsl-apt:5.1.0"
			dependency "io.rest-assured:rest-assured:5.5.1"
			dependency "org.awaitility:awaitility:4.3.0"
			dependency "org.mockito:mockito-inline:5.2.0"
		}
	}

	dependencies {
		components {
			withModule("com.querydsl:querydsl-jpa") { details ->
				details.allVariants {
					withFiles {
						removeAllFiles()
						addFile("querydsl-jpa-${details.id.version}-jakarta.jar")
					}
				}
			}
			withModule("com.querydsl:querydsl-apt") { details ->
				details.allVariants {
					withFiles {
						removeAllFiles()
						addFile("querydsl-apt-${details.id.version}-jakarta.jar")
					}
				}
			}
		}
	}
}

dependencies {
	implementation project(':modules:_common')
	implementation project(':modules:identity')
	implementation project(':modules:ai')
	implementation project(':modules:payment')
	implementation project(':modules:transaction')
	implementation project(':modules:wallet')

	// spring-web/core
	implementation('org.springframework.boot:spring-boot-starter-webflux')

	// util
	implementation('com.google.guava:guava')
	annotationProcessor('org.projectlombok:lombok')
	compileOnly('org.projectlombok:lombok')

	// test related
    testImplementation project(path: ':modules:_common', configuration: 'testArtifacts')
	testRuntimeOnly('org.junit.platform:junit-platform-launcher')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.security:spring-security-test')
	testImplementation('org.springframework.boot:spring-boot-testcontainers')
	testImplementation("org.testcontainers:postgresql")
	testImplementation('org.testcontainers:junit-jupiter')
	testCompileOnly("org.projectlombok:lombok")
	testAnnotationProcessor("org.projectlombok:lombok")
	testImplementation("io.rest-assured:rest-assured")
	testImplementation("org.awaitility:awaitility")
	testImplementation("org.mockito:mockito-inline")
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-parameters")
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}
